10 REM SKIFF by FRANCESCO FIORENTINI - Gennaio 2020
15 GOSUB 600:REM DISEGNA BANDIERA E ALBERO

24 REM DEFINISCE OGGETTI
25 SHIP$=" "+CHR$(248)+" "
26 TREE$=CHR$(255)
27 STONE$=CHR$(253)

30 REM INIZIALIZZA I PARAMETRI DEL GIOCO
35 GOSUB 2000:REM MENU INIZIALE
36 GOSUB 2400:REM PARAMETRI LIVELLO

38 REM INIZIALIZZA I VALORI DEL GIOCO
40 SCORE=0
41 DSPFLAG=0
45 DOORS=0
50 FLINE=3050
55 MODE 1
60 BORDER 26
70 SHIPX=18
80 YY=1
100 PAPER 0
110 INK 0,26
115 INK 1,0
120 CLS

130 REM MAIN LOOP
140 A=INT(RND(1)*38+1)
150 PEN 1
155 LOCATE A,25:PRINT TREE$;
159 REM ROCCE SOLO PER LIVELLO M-H-I
160 IF LVL$="R" OR LVL$="r" THEN GOTO 180
162 IF LVL$="E" OR LVL$="e" THEN GOTO 180
170 B=INT(RND(1)*(38-A)+1)
172 LOCATE B,25:PRINT STONE$
180 IF DSPFLAG=VDOORS THEN GOSUB 1000:REM DISTANZA PORTE IN VERTICALE
186 DSPFLAG=DSPFLAG+1
187 LOCATE 40,25:PRINT" "
190 REM MOVE SKI
200 SCORE=SCORE+10
210 IF INKEY(8)=0 AND SHIPX>1 THEN SHIPX=SHIPX-1
220 IF INKEY(1)=0 AND SHIPX<38 THEN SHIPX=SHIPX+1
230 XX=SHIPX+1
235 REM CONTROLLO COLLISIONI
240 LOCATE XX,YY
245 DD$=COPYCHR$(#0)
250 IF DD$=CHR$(255) THEN GOTO 300
252 IF DD$=CHR$(253) THEN GOTO 350
255 IF DD$=CHR$(95) THEN DOORS=DOORS+1
260 IF SCORE = FLINE THEN LOCATE 1,25:PRINT " = = = = = = F I N I S H = = = = = = ="
265 IF SCORE = FLINE+250 THEN GOSUB 1600
280 LOCATE SHIPX,1
281 PEN 1
282 PRINT SHIP$
290 GOTO 130

300 REM UCCISO DA UN ALBERO
302 PAPER 1
304 PEN 3
305 PRINT CHR$(7)
306 LOCATE 14,6:PRINT"Oh Cavolo..."
308 FOR Q=1 TO 1000: NEXT Q
310 LOCATE 7, 8:PRINT"Sei finito contro un albero..."
312 LOCATE 5, 9:PRINT"Come sciatore sei proprio scarso!"
314 FOR q=1 TO 1000: NEXT q
316 LOCATE 7, 12: PRINT "Hai percorso: " ;SCORE;" metri"
318 LOCATE 7, 13: PRINT "Passando solo: " ;DOORS;" porte!"
320 IF INKEY$=" " THEN GOTO 320
322 IF INKEY$<>" " THEN GOTO 322
324 GOTO 30

350 REM UCCISO DA UNA ROCCIA
352 PAPER 1
354 PEN 3
355 PRINT CHR$(7)
356 LOCATE 14,6:PRINT"Oh Cavolo..."
358 FOR Q=1 TO 1000: NEXT Q
360 LOCATE 7, 8:PRINT"Sei finito contro una roccia..."
362 LOCATE 5, 9:PRINT"Come sciatore sei proprio scarso!"
364 FOR q=1 TO 1000: NEXT q
366 LOCATE 7, 12: PRINT "Hai percorso: " ;SCORE;" metri"
368 LOCATE 7, 13: PRINT "Passando solo: " ;DOORS;" porte!"
370 IF INKEY$=" " THEN GOTO 370
372 IF INKEY$<>" " THEN GOTO 322
374 GOTO 30

600 REM DISEGNA LA BANDIERINA RIDEFINENDO IL CHAR(254)
605 SYMBOL 254,64,112,124,126,64,64,64,64
610 REM DISEGNA UN ALBERO RIDEFINENDO IL  CHAR(255)
615 SYMBOL 255,24,60,24,126,24,255,24,24
620 REM DISEGNA UNA ROCCIARIDEFINENDO IL CHAR(253)
625 SYMBOL 253,0,0,0,24,60,124,127,255
630 RETURN

1000 REM DISEGNA LE BANDIERINE ED IL CARATTERE DI CONTROLLO
1001 REM PER CONTARE LE PORTE
1005 C=INT(RND(1)*(LSUP-LINF)+LINF)
1010 PEN 3
1020 LOCATE C,25:PRINT GAP$
1040 DSPFLAG=0
1050 RETURN

1600 REM LIVELLO COMPLETATO
1610 PAPER 1
1620 PEN 3
1630 LOCATE 15,6:PRINT"BRAVO!!!!"
1640 FOR Q=1 TO 1000: NEXT Q
1650 LOCATE 7, 8:PRINT"Hai completato il livello..."
1660 FOR q=1TO 1000: NEXT q
1670 LOCATE 8, 11: PRINT "Hai percorso : " ;SCORE;" metri"
1675 LOCATE 9, 13: PRINT "Hai passato : " ;DOORS;" porte"
1677 MISSED = INT(FLINE/(VDOORS*10)) - DOORS
1678 LOCATE 9, 14: PRINT "Hai mancato : " ;MISSED;" porte"
1679 LOCATE 7, 16:PRINT"Un'altra sfida? (premi SPAZIO)"
1680 IF INKEY$=" " THEN GOTO 1680
1690 IF INKEY$<>" " THEN GOTO 1690
1700 GOTO 30

2000 REM MENU INIZIALE
2005 CLS
2010 LOCATE 10,2:PRINT CHR$(143);CHR$(143);CHR$(143);"  ";CHR$(143);"  ";CHR$(143);"  ";CHR$(143);"  ";CHR$(143);CHR$(143);CHR$(143);"  ";CHR$(143);CHR$(143);CHR$(143)
2015 LOCATE 10,3:PRINT CHR$(143);"    ";CHR$(143);" ";CHR$(143);"   ";CHR$(143);"  "CHR$(143);"    "CHR$(143)
2020 LOCATE 10,4:PRINT " ";CHR$(143);"   ";CHR$(143);CHR$(143);"    ";CHR$(143);"  ";CHR$(143);CHR$(143);"   ";CHR$(143);CHR$(143)
2025 LOCATE 10,5:PRINT "  ";CHR$(143);"  ";CHR$(143);" ";CHR$(143);"   ";CHR$(143);"  "CHR$(143);"    "CHR$(143)
2030 LOCATE 10,6:PRINT CHR$(143);CHR$(143);CHR$(143);"  ";CHR$(143);"  ";CHR$(143);"  ";CHR$(143);"  "CHR$(143);"    "CHR$(143)
2040 LOCATE 15,8:PRINT "di Francesco Fiorentini"
2050 LOCATE 10,12:PRINT "Livello:"
2051 LOCATE 10,13:PRINT "(R)ookie"
2052 LOCATE 10,14:PRINT "(E)asy"
2053 LOCATE 10,15:PRINT "(M)edium"
2054 LOCATE 10,16:PRINT "(H)ard"
2055 LOCATE 10,17:PRINT "(I)mpossible"
2056 LOCATE 1,20:PRINT "Istruzioni:"
2058 LOCATE 1,21:PRINT "Passa tra le porte evita alberi e rocce"
2057 LOCATE 1,22:PRINT "Usa le frecce ";CHR$(242);CHR$(243);" per spostarti"
2100 LVL$= INKEY$
2102 IF LVL$="R" OR LVL$="r" THEN GOTO 2120
2104 IF LVL$="E" OR LVL$="e" THEN GOTO 2120
2106 IF LVL$="M" OR LVL$="m" THEN GOTO 2120
2108 IF LVL$="H" OR LVL$="h" THEN GOTO 2120
2110 IF LVL$="I" OR LVL$="i" THEN GOTO 2120
2115 GOTO 2100
2120 RETURN

2400 REM INIZIALIZZA VALORI PER LIVELLO
2401 IF LVL$="R" OR LVL$="r" THEN GOTO 2500
2402 IF LVL$="E" OR LVL$="e" THEN GOTO 2600
2403 IF LVL$="M" OR LVL$="m" THEN GOTO 2700
2404 IF LVL$="H" OR LVL$="h" THEN GOTO 2800
2405 IF LVL$="I" OR LVL$="i" THEN GOTO 2900
2500 REM LIVELLO ROOKIE 
2502 GAP$=CHR$(254)+"______"+CHR$(254)
2503 VDOORS=15
2504 LINF=10:REM LIMITE INF DISTANZA H PORTE
2505 LSUP=25:REM LIMITE SUP DISTANZA H PORTE
2599 RETURN
2600 REM LIVELLO EASY 
2602 GAP$=CHR$(254)+"_____"+CHR$(254)
2603 VDOORS=15
2604 LINF=9
2605 LSUP=27
2699 RETURN
2700 REM LIVELLO MEDIUM 
2702 GAP$=CHR$(254)+"____"+CHR$(254)
2703 VDOORS=14
2704 LINF=8
2705 LSUP=29
2799 RETURN
2800 REM LIVELLO HARD 
2802 GAP$=CHR$(254)+"___"+CHR$(254)
2803 VDOORS=14
2804 LINF=8
2805 LSUP=31
2899 RETURN
2900 REM LIVELLO IMPOSSIBLE 
2902 GAP$=CHR$(254)+"__"+CHR$(254)
2903 VDOORS=13
2904 LINF=7
2905 LSUP=33
2999 RETURN
